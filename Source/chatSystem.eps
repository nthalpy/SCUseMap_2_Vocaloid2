import answer;
import _;

const pStringBuffer = Db(300);

function replaceChat(answerIndex) {
    for (var line = 0; line < 11; line++) {
        var pChat = 0x640B60 + line * 218;

        // String is empty.
        if (bread(pChat) == 0) 
            continue;
        
        var len = strlen(pChat);
        // String has already processed.
        if (bread(pChat + len - 1) == 0x1D) 
            continue;        

        foreach (cp : EUDLoopPlayer("Human")) {
            dbstr_print(pStringBuffer, PName(cp), ":");

            var matches = true;
            for (var i = 0; i < strlen(pStringBuffer); i++) {
                if (bread(pStringBuffer + i) != bread(pChat + i)) {
                    matches = false;
                    break;
                }
            }

            if (matches == false)
                continue;
        
            // PName, colon, space, white color code
            var pContentSrc = pChat + (strlen(pStringBuffer) - 1) + 3;
            var match = answer.checkAnswerPtr(pContentSrc, answerIndex);
            
            if (match == answer.checkAnswer_Title) {
                dbstr_print(pChat, ptr2s(pChat), " \x1F <- 제목 정답!", "\x1D");
            }
            else if (match == answer.checkAnswer_Composer) {
                dbstr_print(pChat, ptr2s(pChat), " \x17 <- 작곡가 정답!", "\x1D");
            }
            else {
                dbstr_print(pChat, ptr2s(pChat), "\x1D");
            }
        }
    }
}
